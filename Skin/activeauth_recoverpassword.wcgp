/*

  Copyright 2014 Anton Katsarov <anton@webface.bg>

  Distributed under the MIT License.

  See accompanying file LICENSE or copy at
  http://opensource.org/licenses/MIT

*/

function UnixTimestamp(dateTime) forward;

entry sysEntry is
   theSet = newDictionary();
   sid = GetHTTPParameter("sid",0);
   sessions = GetAccountPreferences("ProntoRestorePasswordSessions");
   if isDictionary(sessions) && isDictionary(sessions.(sid)) then
      cli = ExecuteCLI("GETACCOUNTPREFS " + ObjectToString(sessions.(sid).account));
      if cli == null then
         prefs = Vars().executeCLIResult;
         if Number(UnixTimestamp(GMTTime())) <= Number(sessions.(sid).expires) then
            password = GetHTTPParameter("password",0);
            repassword = GetHTTPParameter("repassword",0);
            if isString(password) && isString(repassword) && password != "" then
               if password == repassword then
                  cli = ExecuteCLI("SETACCOUNTPASSWORD " + ObjectToString(sessions.(sid).account) + " PASSWORD " + ObjectToString(password));
                  if cli == null then
                     theSet.status = "OK";
                     sessions.(sid) = null;
                  else
                     theSet.error = cli;
                  end if;
               else
                  theSet.error = "Passwords do not match";
               end if;
            end if;
         else
            theSet.error = "Session has expired.";
         end if;
         for i = 0  while i < Length(sessions) by i+= 1 loop
            if Number(sessions.(sessions[i]).expires) < Number(UnixTimestamp(GMTTime())) then
               sessions.(sessions[i]) = null;
            end if;
         end loop;
         res = SetAccountPreferences(sessions,"ProntoRestorePasswordSessions");
      else
         theSet.error = "No such account.";
      end if;
   else
      theSet.error = "Invalid session.";
   end if;
   ProcessWSSP("activeauth_recoverpassword", theSet);
end entry;

function UnixTimestamp(dateTime) is
    return DateNumber(dateTime)*86400 + TimeOfDay(dateTime);
end function;
